plugins {
    id 'com.android.library'
    id 'maven-publish'
}

android {
    namespace 'com.jaredrummler.android.colorpicker'
    compileSdkVersion buildConfig.compileSdk
    resourcePrefix "cpv_"
    defaultConfig {
        minSdkVersion buildConfig.minSdk
        targetSdkVersion buildConfig.targetSdk
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation "${deps.androidx.appCompat}"
    implementation "${deps.androidx.preferences}"
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                artifact("$buildDir/outputs/aar/${project.name}-release.aar"){
                    builtBy assembleRelease
                }

                groupId = 'com.github.sagishporer'
                artifactId = 'color-picker-android'

                pom {
                    name = 'Color Picker'
                    description = 'A highly customizable color picker for Android'
                    url = 'https://github.com/sagishporer/color-picker-android'

                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                }
            }
        }
    }
}